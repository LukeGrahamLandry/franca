# test amd64 addressing modes
# for accessing imported data constants
# (qbe's conaddr.ssa was split into isel5.ssa and fault-na.ssa)

export
function w $f0(l %o) {
@start
	%addr =l add $a, %o
	%char =w loadub %addr
	ret %char
}

export
function w $f1(l %o) {
@start
	%o1 =l mul %o, 1
	%addr =l add 10, %o1
	%char =w loadub %addr
	ret %char
}

export
function w $f2(l %o1, l %o2) {
@start
	%o22 =l mul %o2, 2
	%o =l add %o1, %o22
	%addr =l add $a, %o
	%char =w loadub %addr
	ret %char
}

export
function l $f3(l %o) {
@start
	%addr =l add %o, $a
	ret %addr
}

export
function $f4() {
@start
	storel $p, $p
	ret
}

export
function l $f5() {
@start
    %b =l add $a, 1
    ret %b
}

# >>> driver
# #include <stdlib.h>
# char a[] = "qbe rocks";
# void *p;
# extern unsigned f0(long), f1(long), f2(long, long);
# extern char *f3(long);
# extern void f4();
# extern char *f5();
# int main() {
#   int ok = 0;
# 	ok += f0(2) == 'e';
# 	ok += f1((long)a-5) == 'o';
# 	ok += f2(4, 2) == 's';
# 	ok += *f3(0) == 'q';
# 	ok += *f5() == 'b';
# 	f4();
# 	ok += p == &p;
# 	exit(!(ok == 6));
# }
# <<<
