# test amd64 addressing modes
# for accessing imported data constants
# (qbe's conaddr.ssa was split into isel5.ssa and fault-na.ssa)

export
function w $f0(l %o) {
@start
	%addr =l add $a, %o
	%char =w loadub %addr
	ret %char
}

export
function w $f1(l %o) {
@start
	%o1 =l mul %o, 1
	%addr =l add 10, %o1
	%char =w loadub %addr
	ret %char
}

export
function w $f2(l %o1, l %o2) {
@start
	%o22 =l mul %o2, 2
	%o =l add %o1, %o22
	%addr =l add $a, %o
	%char =w loadub %addr
	ret %char
}

export
function l $f3(l %o) {
@start
	%addr =l add %o, $a
	ret %addr
}

export
function $f4() {
@start
	storel $p, $p
	ret
}

export
function l $f5() {
@start
    %b =l add $a, 1
    ret %b
}

# `f6`, `f6fffffffffft`, `f6gggggggggggggggw` have a bucket collission (with my current hash function)
# so when the inner function gets inlined and doesn't show up in the .frc output, reading 
# it back will intern symbols in a different order and only work if mem.offset is remapped correctly. 
# (order these symbols appear also matters)

function l $f6gggggggggggggggw() {
@start
    ret 8
}

export function l $f6() {
@start
    %eight =l call $f6gggggggggggggggw()
    %a =l add $f6fffffffffft, %eight
    %b =l load %a
    ret %b
}

data $f6fffffffffft = { l 123 456 }

# >>> driver
# #include <stdlib.h>
# char a[] = "qbe rocks";
# void *p;
# extern unsigned f0(long), f1(long), f2(long, long), f6();
# extern char *f3(long);
# extern void f4();
# extern char *f5();
# int main() {
#   int ok = 0;
# 	ok += f0(2) == 'e';
# 	ok += f1((long)a-5) == 'o';
# 	ok += f2(4, 2) == 's';
# 	ok += *f3(0) == 'q';
# 	ok += *f5() == 'b';
# 	f4();
# 	ok += p == &p;
# 	ok += f6() == 456;
# 	exit(!(ok == 7));
# }
# <<<
